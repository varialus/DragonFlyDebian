sinclude(./aclocal.m4)dnl Autoconf lossage
GLIBC_PROVIDES dnl See aclocal.m4 in the top level source directory.
#
# Local configure fragment for sysdeps/unix/bsd/bsd4.4/kfreebsd.
# Process this file with "autoconf-2.13 -l ../../../../..".
#

mkdir -p sysdeps/unix/bsd/bsd4.4/kfreebsd

# The kFreeBSD headers can be found in
#   /usr/src/kfreebsd-headers/
# Check whether this directory is available.
if test -d sysdeps/unix/bsd/bsd4.4/kfreebsd/kernel-headers ; then
  sysheaders="sysdeps/unix/bsd/bsd4.4/kfreebsd/kernel-headers"
  ccheaders=`$CC -print-file-name=include`
  dnl We don't have to use -nostdinc.  We just want one more directory
  dnl to be used.
  SYSINCLUDES="-I $sysheaders"
fi
CPPFLAGS="$CPPFLAGS $SYSINCLUDES"

AC_ARG_ENABLE(compatible-utmp, dnl
[  --disable-compatible-utmp  use a struct utmp which is the same as struct
                          utmpx, as on Linux, but incompatible with FreeBSD],
              enable_utmp_compat=$enableval, enable_utmp_compat=no)
if test "$enable_utmp_compat" = no; then
  utmp_subdir=utmp-utmpx
else
  utmp_subdir=utmp-compat
fi

# Add configuration dependent sysdep dirs.
AC_MSG_CHECKING(sysdep dirs)
sysnames=`echo "$sysnames" | sed -e "s| sysdeps/unix/bsd/bsd4.4/kfreebsd/$base_machine | sysdeps/unix/bsd/bsd4.4/kfreebsd/$base_machine sysdeps/unix/bsd/bsd4.4/kfreebsd/kernel-headers/$base_machine |" -e "s| sysdeps/unix/bsd/bsd4.4/kfreebsd | sysdeps/unix/bsd/bsd4.4/kfreebsd sysdeps/unix/bsd/bsd4.4/kfreebsd/kernel-headers/ sysdeps/unix/bsd/bsd4.4/kfreebsd/$utmp_subdir |"`
AC_MSG_RESULT($sysnames)

# Ensure that we don't accidentally access /usr/include while building.
ccheaders=`$CC -print-file-name=include`
SYSINCLUDES="-nostdinc -isystem $ccheaders"

# Don't bother checking for the prerequisites of the ctype glue code,
# since we don't use this glue code.
inhibit_glue=yes

# Put exception handling support into libc, so that not every shared
# library needs to include it.
# FIXME: Does not work yet.
#libc_cv_gcc_unwind_find_fde=yes

if test "$linuxthreads_missing"; then
  if test $enable_sanity = yes; then
    echo "\
*** On GNU/kFreeBSD systems it is normal to compile GNU libc with the
*** 'linuxthreads' add-on.  Without that, the library will be
*** incompatible with normal GNU/kFreeBSD systems.
*** If you really mean to not use this add-on, run configure again
*** using the extra parameter '--disable-sanity-checks'."
    exit 1
  else
    echo "\
*** WARNING: Are you sure you do not want to use the 'linuxthreads'
*** add-on?"
  fi
fi

use_ldconfig=yes
